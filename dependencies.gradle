def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}


ext {
    // Android build tools
    sdkVersion = 26
    minSdkVersion = 19
    buildToolsVersion = '26.0.1'

    // Gradle Plugins
    androidPluginVersion = '3.0.1'
    gradleVersionPluginVersion = '0.17.0'

    // Android Support / Design
    supportLibraryVersion = '26.1.0'
    constraintLayout = '1.0.2'
    androidSupport = dependencyGroup {
        implementation "com.android.support:support-v4:$supportLibraryVersion"
        implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
        implementation "com.android.support:support-annotations:$supportLibraryVersion"
        implementation "com.android.support:design:$supportLibraryVersion"
        implementation "com.android.support:cardview-v7:$supportLibraryVersion"
        implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
        implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
        kapt "com.android.databinding:compiler:$androidPluginVersion"
    }

    // Kotlin
    kotlinVersion = '1.2.0'
    mockitoKotlinVersion = '1.5.0'
    kotlinLoggingVersion = '1.4.6'
    kotlin = dependencyGroup {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
        implementation "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
        testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    }

    // Reactive
    rxJavaVersion = '2.1.1'
    rxAndroidVersion = '2.0.1'
    traceurVersion = "1.0.1"
    rx = dependencyGroup {
        api "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        api "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
        api "com.tspoon.traceur:traceur:$traceurVersion"
    }

    // Debug tools
    leakcanaryVersion = '1.5.1'
    timberVersion = '4.6.0'
    stethoVersion = '1.5.0'
    debugTools = dependencyGroup {
        implementation "com.jakewharton.timber:timber:$timberVersion"
        debugImplementation "com.facebook.stetho:stetho:$stethoVersion"
        debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
        debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
        releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
        testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    }

    // Testing
    junitVersion = '4.12'
    mockitoVersion = '2.13.0'
    elmyrVersion = '0.5'
    assertjVersion = '3.8.0'
    testing = dependencyGroup {
        testImplementation "junit:junit:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "com.github.xgouchet:Elmyr:$elmyrVersion"
        testImplementation "org.assertj:assertj-core:$assertjVersion"
    }


    // Debug Dependencies
    findbugsAnnotationsVersion = '3.0.1'
    javaxInjectVersion = '1'

}